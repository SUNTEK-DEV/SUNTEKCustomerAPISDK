plugins {
    id 'com.android.library'
    id 'maven-publish'
}

android {
    namespace 'com.example.mylibrary'
    compileSdk 34

    defaultConfig {
        minSdk 21
        targetSdk 34
        versionCode 1
        versionName "1.0.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            debuggable true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
}

dependencies {
    implementation libs.appcompat
    implementation libs.material
    testImplementation libs.junit
    androidTestImplementation libs.ext.junit
    androidTestImplementation libs.espresso.core
    // 扫描 libs 目录下的所有 .jar 和 .aar 文件
    implementation fileTree(dir: 'libs', include: ['*.jar','*.aar'])
    implementation 'com.github.SUNTEK-DEV:SdkApiJar:v8.0'
    // 或者，如果只想引入那一个特定的文件
    // implementation files('libs/SdkApiJarV1.250217.0851.aar')
}

// 配置源码任务
//task sourceJar(type: Jar) {
//    from android.sourceSets.main.java.srcDirs
//    archiveClassifier.set("sources")
//}

// 配置Javadoc任务（可选）
//task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
//    from dokkaJavadoc.outputDirectory
//    archiveClassifier.set("javadoc")
//}

// 配置 Maven 发布（用于 JitPack）
afterEvaluate {
    // 配置 Maven 发布（maven-publish）
    publishing {
        publications {
            release(MavenPublication) {
                // 使用 AAR 文件路径
                artifact("$buildDir/outputs/aar/${project.getName()}-release.aar") {
                    classifier = null
                    extension = 'aar'
                }

                groupId = 'com.github.SUNTEK-DEV'
                artifactId = 'SUNTEKCustomerAPISDK'
                version = android.defaultConfig.versionName

                // 配置 POM 依赖
                pom.withXml {
                    def dependenciesNode = asNode().appendNode('dependencies')
                    
                    // 添加所有 implementation 依赖
                    configurations.implementation.allDependencies.each { dep ->
                        if (dep.group != null && dep.name != null && !dep.name.contains('unspecified')) {
                            def dependencyNode = dependenciesNode.appendNode('dependency')
                            dependencyNode.appendNode('groupId', dep.group)
                            dependencyNode.appendNode('artifactId', dep.name)
                            if (dep.version != null) {
                                dependencyNode.appendNode('version', dep.version)
                            }
                        }
                    }
                }
            }
        }
    }
    
    // 创建一个 install 任务（JitPack 会自动识别并执行）
    // 这个任务会先构建 AAR，然后发布到本地 Maven 仓库
    task install(dependsOn: ['assembleRelease', 'publishToMavenLocal']) {
        description = 'Install the library to local Maven repository (for JitPack)'
        group = 'publishing'
    }
}

